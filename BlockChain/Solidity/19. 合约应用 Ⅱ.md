# 荷式拍卖

荷式拍卖（Dutch Auction），也称为减价拍卖。

拍卖从一个较高的起始价格开始，这个价格通常高于市场预期。随着时间的推移，价格按照预先设定的降价阶梯逐步降低。当价格降到某个竞买人愿意接受的水平时，该竞买人出价并成交。第一个应价的竞买人获胜，并支付当时的价格。

1. 定义接口：

```solidity
interface IERC721 {
    function transferFrom(address _from, address _to, uint _nftId) external;
}
```

2. 定义状态变量：

```solidity
contract DutchAuction {
    // 拍卖的持续时间
    uint private constant DURATION = 7 days;

    // 拍卖的 NFT
    IERC721 public immutable nft;

    // 拍卖的 NFT ID
    uint public immutable nftId;

    // 卖家地址
    address payable public immutable seller;

    // 起拍价
    uint public immutable startPrice;

    // 拍卖开始时间
    uint public immutable startTime;

    // 拍卖结束时间
    uint public immutable endTime;

    // 降价幅度
    uint public immutable discountRate;
}
```

3. 实现构造函数：

```solidity
    constructor(
        uint _startPrice,
        uint _discountRate,
        address _nft,
        uint _nftId
    ) {
        require(
            _startPrice >= _discountRate * DURATION,
            "start price must be higher than discount rate * duration"
        );

        seller = payable(msg.sender);
        startPrice = _startPrice;
        discountRate = _discountRate;
        startTime = block.timestamp;
        endTime = startTime + DURATION;
        nft = IERC721(_nft);
        nftId = _nftId;
    }
```

4. 实现函数：

```solidity
    // 查询当前价格
    function getCurrentPrice() public view returns (uint) {
        uint timeElapsed = block.timestamp - startTime; // 拍卖已持续时间
        return startPrice - discountRate * timeElapsed;
    }

    // 购买
    function buy() public payable {
        require(block.timestamp < endTime, "auction has ended");

        uint currentPrice = getCurrentPrice();
        require(msg.value >= currentPrice, "insufficient funds");

        // 将 nftId 代币从卖家转给买家
        nft.transferFrom(seller, msg.sender, nftId);
        uint refund = msg.value - currentPrice;
        if (refund > 0) {
            // 向调用者找零
            payable(msg.sender).transfer(refund);
        }
    }
```

1. 部署 MyNFT 合约（在笔记 "代币.md" 中）

2. 传入编辑器地址、tokenID (这里以 123 为例)，调用 MyNFT 合约的 mint 方法，为编辑器地址 mint 一个 NFT

3. 传入 tokenID (123)，调用 MyNFT 合约的 ownerOf 方法，查看该 NFT 的所有者是否为编辑器地址

4. 传入起拍价 (1000000)、降价幅度 (1)、MyNFT 合约地址、tokenID (123)，部署 DutchAuction 合约

5. 持续调用 DutchAuction 合约的 getCurrentPrice 方法，查看当前价格，可以看到价格在不断下降

6. 传入 DutchAuction 合约地址、tokenID (123)，调用 MyNFT 合约的 approve 方法，授权 DutchAuction 合约管理该 NFT

模拟用户购买：

1. 切换编辑器地址，调用 DutchAuction 合约的 getCurrentPrice 方法，查看当前价格

2. 设置对应的以太数，调用 DutchAuction 合约的 buy 方法，购买 NFT

3. 传入 tokenID (123)，调用 MyNFT 合约的 ownerOf 方法，查看该 NFT 的所有者是否为当前编辑器地址

OK，至此，我们就完成了一次荷式拍卖啦~

<br><br>

# 英式拍卖

英式拍卖（English Auction），也称为增价拍卖。

拍卖从一个较低的起拍价开始。竞买人按竞价阶梯逐步提高出价。每次出价必须高于当前最高出价。当没有人愿意再出更高的价格时，拍卖结束，出价最高者成为买受人。

1. 定义接口：

```solidity
interface IERC721 {
    function transferFrom(address _from, address _to, uint _tokenId) external;
}
```

2. 定义事件：

```solidity
contract EnglishAuction {
    // 当拍卖开始时触发
    event Start();

    // 当有新的出价时触发
    event Bid(address indexed bidder, uint amount);

    // 当退款时触发
    event Refund(address indexed bidder, uint amount);

    // 当拍卖结束时触发
    event End(address highestBidder, uint highestBid);
}
```

3. 定义状态变量：

```solidity
    // 拍卖的 NFT
    IERC721 public immutable nft;

    // 拍卖的 NFT ID
    uint public immutable nftId;

    // 卖家地址
    address payable public immutable seller;

    // 拍卖结束时间
    uint public endTime;

    // 拍卖是否已开始
    bool public isStarted;

    // 拍卖是否已结束
    bool public isEnded;

    // 当前最高出价者
    address public highestBidder;

    // 当前最高出价
    uint public highestBid;

    // 出价记录
    mapping(address => uint) public bids;
```

4. 实现构造函数：

```solidity
    constructor(address _nft, uint _nftId, uint _startBid) {
        nft = IERC721(_nft);
        nftId = _nftId;
        seller = payable(msg.sender);
        highestBid = _startBid;
    }
```

5. 实现函数开始拍卖：

```solidity
    function startAuction() external {
        require(msg.sender == seller, "Only seller can start auction");
        require(!isStarted, "Auction already started");

        isStarted = true;
        endTime = block.timestamp + 7 days;
        // 将 nftId 代币从卖家转给合约
        nft.transferFrom(seller, address(this), nftId);

        emit Start();
    }
```

6. 实现函数出价：

```solidity
    function bid() external payable {
        require(isStarted, "Auction not started");
        require(block.timestamp < endTime && !isEnded, "Auction ended");
        require(msg.sender != seller, "Seller cannot bid");
        require(msg.value > highestBid, "Bid must be higher than highest bid");

        if (highestBidder != address(0)) {
            // 记录上一个最高出价者及其出价, 以便退款
            bids[highestBidder] += highestBid;
        }
        highestBidder = msg.sender;
        highestBid = msg.value;

        emit Bid(msg.sender, msg.value);
    }
```

7. 实现函数结束拍卖：

```solidity
    function endAuction() external {
        require(isStarted, "Auction not started");
        require(!isEnded, "Auction already ended");
        require(block.timestamp >= endTime, "Auction not ended");

        isEnded = true;
        if (highestBidder != address(0)) {
            // 如果有人出价
            // 将 nft 从合约转给最高出价者
            nft.transferFrom(address(this), highestBidder, nftId);
            // 将最高出价转给卖家
            seller.transfer(highestBid);
        } else {
            // 无人出价
            // 将 ntf 从合约转回给卖家
            nft.transferFrom(address(this), seller, nftId);
        }

        emit End(highestBidder, highestBid);
    }
```

8. 实现函数退款：

```solidity
    function refund() external {
        require(msg.sender != highestBidder, "Highest bidder cannot refund");

        uint amount = bids[msg.sender];
        bids[msg.sender] = 0;
        payable(msg.sender).transfer(amount);

        emit Refund(msg.sender, amount);
    }
```

1. 部署 MyNFT 合约（在笔记 "代币.md" 中）

2. 传入编辑器地址、tokenID (这里以 123 为例)，调用 MyNFT 合约的 mint 方法，为编辑器地址 mint 一个 NFT

3. 传入 tokenID (123)，调用 MyNFT 合约的 ownerOf 方法，查看该 NFT 的所有者是否为编辑器地址

4. 传入 MyNFT 合约地址、tokenID (123)、起拍价 (1000000)，部署 EnglishAuction 合约

5. 传入 EnglishAuction 合约地址、tokenID (123)，调用 MyNFT 合约的 approve 方法，授权 EnglishAuction 合约管理该 NFT

模拟卖家开始拍卖：

1. 调用 EnglishAuction 合约的 startAuction 方法，开始拍卖

模拟用户出价：

1. 切换编辑器地址 2，设置以太数 (1000001)，调用 EnglishAuction 合约的 bid 方法，出价

2. 切换编辑器地址 3，设置以太数 (1000002)，调用 EnglishAuction 合约的 bid 方法，出价

模拟卖家结束拍卖：

1. 待拍卖时间结束，调用 EnglishAuction 合约的 endAuction 方法，结束拍卖

2. 传入 tokenID (123)，调用 MyNFT 合约的 ownerOf 方法，查看该 NFT 的所有者是否为当前编辑器地址 3

模拟用户退款：

1. 切换编辑器地址 2，调用 EnglishAuction 合约的 refund 方法，退款

<br><br>
