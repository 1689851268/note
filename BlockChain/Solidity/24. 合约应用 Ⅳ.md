# 金库

简单来说, 金库（Vault）的工作原理: Vault 会将用户存入的资产进行再投资, 例如参与借贷、质押或流动性挖矿等. 通过这些投资活动产生的收益会按比例分配给存款用户.

```solidity
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract MyToken is ERC20 {
    constructor(string memory name, string memory symbol) ERC20(name, symbol) {
        _mint(msg.sender, 1000); // 为部署者 mint 1000 个代币
    }

    function mint(address to, uint256 amount) public {
        _mint(to, amount);
    }
}

contract Vault {
    // 代币实例
    IERC20 public immutable token;

    // 总供应量
    uint public totalSupply;

    // 用户余额
    mapping(address => uint) public balanceOf;

    constructor(address _token) {
        token = IERC20(_token);
    }

    function _mint(address _to, uint _amount) private {
        totalSupply += _amount;
        balanceOf[_to] += _amount;
    }

    function _burn(address _from, uint _amount) private {
        totalSupply -= _amount;
        balanceOf[_from] -= _amount;
    }

    // 存款
    function deposit(uint _amount) public {
        // a - 表示用户存入的代币数量 _amount
        // B - 存之前 合约中的代币数量
        // T - totalSupply
        // s - 需要 mint 的 shares
        // 满足公式 (T + s) / T = (a + B) / B
        // 得 s = a * T / B
        uint shares;
        if (totalSupply == 0) {
            shares = _amount;
        } else {
            shares = (_amount * totalSupply) / token.balanceOf(address(this));
        }
        _mint(msg.sender, shares);
        token.transferFrom(msg.sender, address(this), _amount);
    }

    // 提款
    function withdraw(uint _shares) public {
        // a - 表示用户提取的 shares 数量 _shares
        // B - 提取之前 合约中的代币数量
        // T - totalSupply
        // s - 需要 burn 的代币数量
        // 满足公式 (T - s) / T = (B - a) / B
        // 得 s = a * B / T
        uint amount = (_shares * token.balanceOf(address(this))) / totalSupply;
        _burn(msg.sender, _shares);
        token.transfer(msg.sender, amount);
    }
}
```

1.  传入 "MyToken"、"MT", 部署 MyToken 合约

2.  传入编辑器地址, 查看 MyToken 合约的 balanceOf, 为 1000 wei

3.  传入 MyToken 合约地址, 部署 Vault 合约

4.  传入 Vault 合约地址、1000, 调用 MyToken 合约的 approve 方法, 授权 1000 wei

模拟用户存款:

1.  传入 1000, 调用 Vault 合约的 deposit 方法, 存入 1000 wei

2.  查看 Vault 合约的 totalSupply, 为 1000

3.  复制编辑器地址, 查看 Vault 合约的 balanceOf, 为 1000

模拟金库赚钱了:

1. 传入编辑器地址、1000, 调用 MyToken 合约的 mint 方法, mint 1000 wei

2. 传入 Vault 合约地址、1000, 调用 MyToken 合约的 transfer 方法转账

模拟用户提款:

1. 传入 1000, 调用 Vault 合约的 withdraw 方法, 提取

2. 传入编辑器地址, 查看 MyToken 合约的 balanceOf, 增加了 2000 wei

3. 传入编辑器地址, 查看 Vault 合约的 balanceOf、totalSupply, 均为 0

<br><br>

# CSAMM

简单来说, 恒和自动做市商（CSAMM, Constant Sum Automated Market Maker）的工作原理:

-   用户每次通过 CSAMM 进行代币交换时, 都会支付一定比例的手续费
-   这些手续费会分配给流动性提供者, 作为他们提供流动性的奖励

```solidity
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

contract ConstantSumAMM {
    // 合约中两种代币的实例
    IERC20 public immutable tokenX;
    IERC20 public immutable tokenY;

    // 两种代币的供应量
    uint public supplyX;
    uint public supplyY;

    // 流动池中的总供应量
    uint public totalSupply;

    // 每个用户的流动份额
    mapping(address => uint) public balanceOf;

    constructor(address _tokenX, address _tokenY) {
        tokenX = IERC20(_tokenX);
        tokenY = IERC20(_tokenY);
    }

    // 发行 amount 数量的 token 到 to 账户
    function _mint(address to, uint amount) internal {
        balanceOf[to] += amount;
        totalSupply += amount;
    }

    // 从 from 账户销毁 amount 数量的 token
    function _burn(address from, uint amount) internal {
        balanceOf[from] -= amount;
        totalSupply -= amount;
    }

    // 用一种 token 换取另一种 token
    function swap(
        address _tokenIn,
        uint _amountIn
    ) external returns (uint amountOut) {
        require(
            _tokenIn == address(tokenX) || _tokenIn == address(tokenY),
            "Invalid token"
        );

        // 确定传入的代币为 tokenX 还是 tokenY
        bool isX = _tokenIn == address(tokenX);

        // 根据传入的代币确定输入和输出的代币及其供应量
        (IERC20 tokenIn, IERC20 tokenOut, uint supplyIn, uint supplyOut) = isX
            ? (tokenX, tokenY, supplyX, supplyY)
            : (tokenY, tokenX, supplyY, supplyX);

        // 用户将代币转移到合约中
        tokenIn.transferFrom(msg.sender, address(this), _amountIn);

        // 计算用户实际转移的代币数量
        uint amountIn = tokenIn.balanceOf(address(this)) - supplyIn;

        // 计算用户可以获得的代币数量
        amountOut = (amountIn * 997) / 1000; // 0.3% 手续费

        // 更新两种代币的总供应量
        if (isX) {
            supplyX = supplyIn + amountIn;
            supplyY = supplyOut - amountOut;
        } else {
            supplyY = supplyIn + amountIn;
            supplyX = supplyOut - amountOut;
        }

        // 将代币转移给用户
        tokenOut.transfer(msg.sender, amountOut);
    }

    // 向池中添加流动性, 获得相应的份额
    function addLiquidity(
        uint amountX,
        uint amountY
    ) external returns (uint shares) {
        // 用户将代币转移到合约中
        tokenX.transferFrom(msg.sender, address(this), amountX);
        tokenY.transferFrom(msg.sender, address(this), amountY);

        // 计算用户实际转移的代币数量
        uint _amountX = tokenX.balanceOf(address(this)) - supplyX;
        uint _amountY = tokenY.balanceOf(address(this)) - supplyY;

        // 计算用户可以获得的份额
        // a - amount in
        // L - total liquidity
        // s - shares to mint
        // T - total supply
        // 需满足 (a + L) / L = (s + T) / T
        // 所以 s = a * T / L
        if (totalSupply == 0) {
            // 对于第一次添加流动性的用户, 直接按照转移的代币数量计算份额
            shares = _amountX + _amountY;
        } else {
            shares =
                ((_amountX + _amountY) * totalSupply) /
                (supplyX + supplyY);
        }

        // 发行份额给用户
        require(shares > 0, "Invalid shares");
        _mint(msg.sender, shares);

        // 更新两种代币的总供应量
        supplyX += _amountX;
        supplyY += _amountY;
    }

    // 根据持有的份额提取流动性
    function removeLiquidity(
        uint shares
    ) external returns (uint amountX, uint amountY) {
        // 计算用户可以获得的代币数量
        // a - amount out
        // L - total liquidity
        // s - shares
        // T - total supply
        // 需满足 a / L = s / T
        // 所以 a = s * L / T
        amountX = (shares * supplyX) / totalSupply;
        amountY = (shares * supplyY) / totalSupply;

        // 销毁用户的份额
        _burn(msg.sender, shares);

        // 更新两种代币的总供应量
        supplyX -= amountX;
        supplyY -= amountY;

        // 将代币转移给用户
        require(amountX > 0 && amountY > 0, "Invalid amount");
        tokenX.transfer(msg.sender, amountX);
        tokenY.transfer(msg.sender, amountY);
    }
}
```

编写测试合约:

```solidity
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract MyToken is ERC20 {
    constructor(string memory name, string memory symbol) ERC20(name, symbol) {}

    function mint(address to, uint amount) external {
        _mint(to, amount);
    }
}
```

1. 部署两次 MyToken 合约, 分别作为两种代币, 这里分别命名为 `tokenX` 和 `tokenY`, 符号分别为 `X` 和 `Y`

2. 传入 `tokenX` 和 `tokenY` 的地址, 部署 ConstantSumAMM 合约

3. 传入编辑器地址和 `1000`, 调用 `tokenX` 合约的 mint 函数, 表示在 `tokenX` 合约中发行 `1000` 个代币到编辑器地址

4. 传入编辑器地址和 `1000`, 调用 `tokenY` 合约的 mint 函数, 表示在 `tokenY` 合约中发行 `1000` 个代币到编辑器地址

5. 传入 ConstantSumAMM 合约的地址和 `1000`, 调用 `tokenX` 合约的 approve 函数, 表示授权 ConstantSumAMM 合约可以从编辑器地址转移 `1000` 个 `tokenX` 代币

6. 传入 ConstantSumAMM 合约的地址和 `1000`, 调用 `tokenY` 合约的 approve 函数, 表示授权 ConstantSumAMM 合约可以从编辑器地址转移 `1000` 个 `tokenY` 代币

**测试 addLiquidity**:

1. 传入 `1000` 和 `1000`, 调用 ConstantSumAMM 合约的 addLiquidity 函数, 表示编辑器地址向 ConstantSumAMM 合约添加 `1000` 个 `tokenX` 和 `1000` 个 `tokenY`, 获取响应的流动性份额

2. 可以查看到 ConstantSumAMM 合约的池中总的流动性份额 totalSupply 为 `2000`、两种代币的总供应量 supplyX、supplyY 均为 `1000`；传入编辑器地址, 查看到用户的流动性份额 balanceOf 为 `2000`

3. 查看 `tokenX` 和 `tokenY` 合约中编辑器地址的余额, 可以看到编辑器地址的 `tokenX`、`tokenY` 的余额均变成了 `0`

**测试 swap**:

1. 切换编辑器地址 2

2. 传入编辑器地址 2 和 `1000`, 调用 `tokenX` 合约的 mint 函数, 表示在 `tokenX` 合约中发行 `1000` 个代币到编辑器地址 2

3. 传入 ConstantSumAMM 合约的地址和 `1000`, 调用 `tokenX` 合约的 approve 函数, 表示授权 ConstantSumAMM 合约可以从编辑器地址 2 转移 `1000` 个 `tokenX` 代币

4. 查看 `tokenX` 合约中编辑器地址 2 的余额, 可以看到编辑器地址 2 的 `tokenX` 余额为 `1000`

5. 传入 `tokenX` 的地址和 `1000`, 调用 ConstantSumAMM 合约的 swap 函数, 表示编辑器地址 2 用 `1000` 个 `tokenX` 交换 `tokenY`

6. 查看 `tokenX` 和 `tokenY` 合约中编辑器地址 2 的余额, 可以看到编辑器地址 2 的 `tokenX` 余额变成了 `0`、`tokenY` 余额变成了 `997`, 表示编辑器地址 2 用 `1000` 个 `tokenX` 交换了 `997` 个 `tokenY`

7. 查看 ConstantSumAMM 合约的池中总的流动性份额 totalSupply 仍为 `2000`、两种代币的总供应量 supplyX 为 `2000`、supplyY 为 `3`

**测试 removeLiquidity**:

1. 切换回编辑器地址 1

2. 传入 `2000`, 调用 ConstantSumAMM 合约的 removeLiquidity 函数, 表示编辑器地址提取 `2000` 份额的流动性, 获得相应的代币数量

3. 可以查看到 ConstantSumAMM 合约的池中总的流动性份额 totalSupply 为 `0`、两种代币的总供应量 supplyX 为 `0`、supplyY 为 `0`；传入编辑器地址, 查看到用户的流动性份额 balanceOf 为 `0`

4. 查看 `tokenX` 和 `tokenY` 合约中编辑器地址的余额, 可以看到用户的 `tokenX` 余额变成了 `2000`、`tokenY` 余额变成了 `3`

<br><br>
