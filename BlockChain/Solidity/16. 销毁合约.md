# selfdestruct 命令

selfdestruct 命令用于删除智能合约, 并将合约中剩余的 ETH 转移到指定地址. 它最初被命名为 suicide (自杀) , 但由于这个词过于敏感, 后来改名为 selfdestruct.

在 Solidity v0.8.18 版本中, selfdestruct 被标记为「不再建议使用」, 因为它在某些情况下会导致意外的合约行为. 尽管目前没有替代方案, 但开发者在编译阶段会收到警告, 详细信息请参阅 [EIP-6049](https://eips.ethereum.org/EIPS/eip-6049).

在以太坊坎昆 (Cancun) 升级中, [EIP-6780](https://eips.ethereum.org/EIPS/eip-6780) 被纳入升级以更好地支持 Verkle Tree. 根据提案描述, selfdestruct 仅会被用来将合约中的 ETH 转移到指定地址, 而原先的删除功能只有在 "合约创建" "自毁" 这两个操作处在同一笔交易时才能生效. 所以目前来说:

1. 已经部署的合约无法被 selfdestruct 删除.

2. 如果要使用原先的 selfdestruct 功能, 必须在同一笔交易中创建并 selfdestruct 合约.

**如何使用 selfdestruct**:

```solidity
selfdestruct(_addr);
```

其中 `_addr` 是接收合约中剩余 ETH 的地址. 该地址不需要有 receive 或 fallback 函数也能接收 ETH .

**Demo - 转移 ETH**:

以下合约在坎昆升级前可以完成合约的自毁, 在坎昆升级后仅能实现 ETH 的转移.

```solidity
contract DeleteContract {
    uint public value = 10;

    constructor() payable {}

    function deleteContract() external {
        // 调用 selfdestruct 销毁合约, 并把剩余的 ETH 转给 msg.sender
        selfdestruct(payable(msg.sender));
    }

    function getBalance() external view returns (uint balance) {
        balance = address(this).balance;
    }
}
```

1. 部署合约并转入 10 wei, 调用 getBalance 函数查看合约余额为 10 wei .

2. 调用 deleteContract 函数销毁合约.

3. 在坎昆升级前, 合约会被自毁, 查看 value 为 0 ; 在升级后, 合约依然存在, 只是将合约包含的 ETH 转移到指定地址, 而合约依然能够调用, 查看 value 为 10 .

**Demo - 同笔交易内实现 "合约创建" "自毁"**:

根据提案 EIP-6049, 原先的删除功能只有在 "合约创建" "自毁" 这两个操作处在同一笔交易时才能生效. 所以我们需要通过另一个合约进行控制.

```solidity
contract DeployContract {
    struct DemoResult {
        address addr;
        uint balance;
        uint value;
    }

    constructor() payable {}

    function getBalance() external view returns (uint balance) {
        balance = address(this).balance;
    }

    function demo() public payable returns (DemoResult memory) {
        DeleteContract del = new DeleteContract{value: msg.value}();
        DemoResult memory res = DemoResult({
            addr: address(del),
            balance: del.getBalance(),
            value: del.value()
        });
        del.deleteContract();
        return res;
    }
}
```

1. 部署 DeployContract 合约并转入 100 wei

2. 设置以太币为 50 wei, 调用 DeployContract 合约的 demo 函数. 在同一笔交易中, 创建 DeleteContract 合约并销毁. 查看返回值, 显示 DeleteContract 地址,合约余额 50 wei (selfdestruct 后 ETH 转移到 DeployContract) ,value 为 10 .

3. 导入返回值中的地址为 DeleteContract 合约, 查看合约余额为 0 wei,value 为 0 (表示合约已被销毁).

**注意事项**:

1.  对外提供合约销毁接口时, 最好设置为只有合约所有者可以调用.

2.  当合约中有 selfdestruct 功能时常常会带来安全问题和信任问题. 此外, 此功能还会降低用户对合约的信心.

<br><br>
