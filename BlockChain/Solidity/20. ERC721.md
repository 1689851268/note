# ERC165

ERC165 是以太坊的一种标准, 用于检测智能合约是否实现了特定的接口.

**接口标识符 (Interface Identifier)**

接口标识符是接口中所有函数选择器的异或 (XOR) 结果.

**IERC165 接口合约**

只声明了一个 `supportsInterface` 函数:

```solidity
interface IERC165 {
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}
```

**ERC721 中的实现**

```solidity
    function supportsInterface(
        bytes4 interfaceId
    ) external pure override returns (bool) {
        return
            interfaceId == type(IERC721).interfaceId ||
            interfaceId == type(IERC165).interfaceId;
    }
```

当查询的 `interfaceId` 是 IERC721 或 IERC165 时, 函数返回 `true`；否则返回 `false`.

<br><br>

# IERC721

IERC721 是 ERC721 标准的接口合约, 规定了 ERC721 要实现的基本函数. 它利用 `tokenId` 来表示特定的非同质化代币 (NFT), 授权或转账都需要明确 `tokenId`.

```solidity
interface IERC721 is IERC165 {
    // 返回某地址的 NFT 持有量
    function balanceOf(address owner) external view returns (uint256 balance);

    // 返回某 tokenId 的拥有者地址
    function ownerOf(uint256 tokenId) external view returns (address owner);

    // 将 tokenId 从 from 转移到 to
    function transferFrom(address from, address to, uint256 tokenId) external;

    // 将 tokenId 从 from 安全地转移到 to (如果 to 是合约, 会要求实现 ERC721Receiver 接口)
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    // safeTransferFrom 函数重载, 添加了 data 参数
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes calldata data
    ) external;

    // 授权地址 to 可以转移 tokenId
    function approve(address to, uint256 tokenId) external;

    // 将自己持有的 NFT 批量授权给地址 operator
    function setApprovalForAll(address operator, bool _approved) external;

    // 返回 tokenId 的授权地址
    function getApproved(
        uint256 tokenId
    ) external view returns (address operator);

    // 返回地址 owner 是否批量授权给地址 operator
    function isApprovedForAll(
        address owner,
        address operator
    ) external view returns (bool);
}
```

```solidity
    // 在转账时被释放, 记录代币的发出地址 from, 接收地址 to 和 tokenId
    event Transfer(
        address indexed from,
        address indexed to,
        uint256 indexed tokenId
    );

    // 在授权时释放, 记录授权地址 owner, 接收地址 approved 和 tokenId
    event Approval(
        address indexed owner,
        address indexed approved,
        uint256 indexed tokenId
    );

    // 在批量授权时释放, 记录授权地址 owner, 接收地址 operator 和是否授权 approved
    event ApprovalForAll(
        address indexed owner,
        address indexed operator,
        bool approved
    );
```

<br><br>

# IERC721Receiver

如果一个合约没有实现 ERC721 的相关函数, 转入的 NFT 就会进入黑洞, 永远无法转出. 为了防止这种情况, ERC721 实现了 `safeTransferFrom` 安全转账函数. 目标合约必须实现 IERC721Receiver 接口才能接收 ERC721 代币, 否则交易会被回滚 (revert).

IERC721Receiver 接口只包含一个 `onERC721Received` 函数:

```solidity
interface IERC721Receiver {
    function onERC721Received(
        address operator,
        address from,
        uint tokenId,
        bytes calldata data
    ) external returns (bytes4);
}
```

ERC721 利用 `_checkOnERC721Received` 函数来确保目标合约实现了 `onERC721Received` 函数 (返回 onERC721Received 的 selector) :

```solidity
    function _checkOnERC721Received(
        address operator,
        address from,
        address to,
        uint256 tokenId,
        bytes memory data
    ) internal {
        // 判断目标地址 to 是否为合约地址
        if (to.code.length > 0) {
            try
                // 尝试调用 IERC721Receiver(to).onERC721Received 函数
                IERC721Receiver(to).onERC721Received(
                    operator,
                    from,
                    tokenId,
                    data
                )
            returns (bytes4 retVal) {
                // 如果返回值不是 IERC721Receiver.onERC721Received.selector, 则回滚交易
                if (retVal != IERC721Receiver.onERC721Received.selector) {
                    revert IERC721Errors.ERC721InvalidReceiver(to);
                }
            } catch (bytes memory reason) {
                // 如果调用失败且没有返回任何原因, 则回滚交易
                if (reason.length == 0) {
                    revert IERC721Errors.ERC721InvalidReceiver(to);
                } else {
                    // 如果有返回原因, 则使用内联汇编回滚交易
                    /// @solidity memory-safe-assembly
                    assembly {
                        revert(add(32, reason), mload(reason))
                    }
                }
            }
        }
    }
```

<br><br>

# IERC721Metadata

IERC721Metadata 是 ERC721 的拓展接口, 实现了 3 个查询元数据 (metadata) 的常用函数:

```solidity
interface IERC721Metadata is IERC721 {
    // 代币名称
    function name() external view returns (string memory);

    // 代币代号
    function symbol() external view returns (string memory);

    // 通过 tokenId 查询 metadata 的链接 URI, 这是 ERC721 特有的函数
    function tokenURI(uint256 tokenId) external view returns (string memory);
}
```

<br><br>

# ERC721 主合约

ERC721 主合约实现了 IERC721、IERC165 和 IERC721Metadata 定义的所有功能, 包含 4 个状态变量和 17 个函数.

```solidity
contract ERC721 is IERC721, IERC721Metadata {
    using Strings for uint256; // 使用 String 库

    // Token 名称
    string public override name;

    // Token 代号
    string public override symbol;

    // 记录每个 tokenId 对应的拥有者地址
    mapping(uint => address) private _owners;

    // 记录每个地址持有的代币数量
    mapping(address => uint) private _balances;

    // 记录每个 tokenId 的授权地址
    mapping(uint => address) private _tokenApprovals;

    // 记录每个拥有者地址对操作员地址的批量授权情况
    mapping(address => mapping(address => bool)) private _operatorApprovals;

    // 实现 IERC165 接口的 supportsInterface 函数, 用于检测合约是否实现了特定的接口
    function supportsInterface(
        bytes4 interfaceId
    ) external pure override returns (bool) {
        return
            interfaceId == type(IERC721).interfaceId ||
            interfaceId == type(IERC165).interfaceId ||
            interfaceId == type(IERC721Metadata).interfaceId;
    }

    // 实现 IERC721 接口的 balanceOf 函数, 用于查询某地址持有的 NFT 数量
    function balanceOf(address owner) external view override returns (uint) {
        require(owner != address(0), "owner = zero address");
        return _balances[owner];
    }

    // 实现 IERC721 接口的 ownerOf 函数, 用于查询某个 tokenId 的拥有者地址
    function ownerOf(
        uint tokenId
    ) public view override returns (address owner) {
        owner = _owners[tokenId];
        require(owner != address(0), "token doesn't exist");
    }

    // 实现 IERC721 接口的 isApprovedForAll 函数, 用于查询某地址是否将其所有的 NFT 批量授权给另一个地址
    function isApprovedForAll(
        address owner,
        address operator
    ) external view override returns (bool) {
        return _operatorApprovals[owner][operator];
    }

    // 实现 IERC721 的 setApprovalForAll, 利用 _setApprovalForAll 函数将持有代币全部授权给 operator 地址
    function setApprovalForAll(
        address operator,
        bool approved
    ) external override {
        _operatorApprovals[msg.sender][operator] = approved;
        emit ApprovalForAll(msg.sender, operator, approved);
    }

    // 实现 IERC721 接口的 getApproved 函数, 用于查询某个 tokenId 的授权地址
    function getApproved(
        uint tokenId
    ) external view override returns (address) {
        require(_owners[tokenId] != address(0), "token doesn't exist");
        return _tokenApprovals[tokenId];
    }

    // 内部授权函数, 通过调整 _tokenApprovals 来授权 to 地址操作 tokenId, 并释放 Approval 事件
    function _approve(address owner, address to, uint tokenId) private {
        _tokenApprovals[tokenId] = to;
        emit Approval(owner, to, tokenId);
    }

    // 实现 IERC721 接口的 approve 函数, 将 tokenId 授权给 to 地址
    function approve(address to, uint tokenId) external override {
        address owner = _owners[tokenId];
        // 条件: to 不是 owner
        require(to != owner, "to = owner");
        // 条件: msg.sender 是 owner 或授权地址
        require(
            msg.sender == owner || _operatorApprovals[owner][msg.sender],
            "not owner nor approved for all"
        );
        // 调用 _approve 函数
        _approve(owner, to, tokenId);
    }

    // 检查 spender 地址是否被授权使用 tokenId, 即 spender 是否是 owner 或被授权的地址
    function _isApprovedOrOwner(
        address owner,
        address spender,
        uint tokenId
    ) private view returns (bool) {
        return (spender == owner ||
            _tokenApprovals[tokenId] == spender ||
            _operatorApprovals[owner][spender]);
    }

    // 执行 tokenId 的转账操作, 通过调整 _balances 和 _owners 变量将 tokenId 从 from 转移到 to, 并释放 Transfer 事件
    function _transfer(
        address owner,
        address from,
        address to,
        uint tokenId
    ) private {
        // 条件: from 是 owner
        require(from == owner, "not owner");
        // 条件: to 不是 0 地址
        require(to != address(0), "transfer to the zero address");

        _approve(owner, address(0), tokenId);

        _balances[from] -= 1;
        _balances[to] += 1;
        _owners[tokenId] = to;

        emit Transfer(from, to, tokenId);
    }

    // 实现 IERC721 接口的 transferFrom 函数, 执行非安全转账操作
    function transferFrom(
        address from,
        address to,
        uint tokenId
    ) external override {
        address owner = ownerOf(tokenId);
        require(
            _isApprovedOrOwner(owner, msg.sender, tokenId),
            "not owner nor approved"
        );
        _transfer(owner, from, to, tokenId);
    }

    // 执行安全转账操作, 确保目标合约支持 ERC721 协议, 以防止代币被永久锁定
    function _safeTransfer(
        address owner,
        address from,
        address to,
        uint tokenId,
        bytes memory _data
    ) private {
        _transfer(owner, from, to, tokenId);
        _checkOnERC721Received(from, to, tokenId, _data);
    }

    // 实现 IERC721 接口的 safeTransferFrom 函数, 执行安全转账操作
    function safeTransferFrom(
        address from,
        address to,
        uint tokenId,
        bytes memory _data
    ) public override {
        address owner = ownerOf(tokenId);
        require(
            _isApprovedOrOwner(owner, msg.sender, tokenId),
            "not owner nor approved"
        );
        _safeTransfer(owner, from, to, tokenId, _data);
    }

    // safeTransferFrom 函数的重载版本, 不带附加数据 _data
    function safeTransferFrom(
        address from,
        address to,
        uint tokenId
    ) external override {
        safeTransferFrom(from, to, tokenId, "");
    }

    // 铸造新的 tokenId 并将其转移给 to 地址, 同时调整 _balances 和 _owners 变量, 并释放 Transfer 事件
    // 当前实现允许任何人铸造代币, 实际应用中通常需要重写此函数以限制铸造权限
    function _mint(address to, uint tokenId) internal virtual {
        require(to != address(0), "mint to zero address");
        require(_owners[tokenId] == address(0), "token already minted");

        _balances[to] += 1;
        _owners[tokenId] = to;

        emit Transfer(address(0), to, tokenId);
    }

    // 销毁指定的 tokenId, 同时调整 _balances 和 _owners 变量, 并释放 Transfer 事件
    function _burn(uint tokenId) internal virtual {
        address owner = ownerOf(tokenId);
        require(msg.sender == owner, "not owner of token");

        _approve(owner, address(0), tokenId);

        _balances[owner] -= 1;
        delete _owners[tokenId];

        emit Transfer(owner, address(0), tokenId);
    }

    // 确保目标合约实现了 onERC721Received 函数, 以防止代币被永久锁定
    function _checkOnERC721Received(
        address from,
        address to,
        uint256 tokenId,
        bytes memory data
    ) private {
        if (to.code.length > 0) {
            try
                IERC721Receiver(to).onERC721Received(
                    msg.sender,
                    from,
                    tokenId,
                    data
                )
            returns (bytes4 retval) {
                if (retval != IERC721Receiver.onERC721Received.selector) {
                    revert ERC721InvalidReceiver(to);
                }
            } catch (bytes memory reason) {
                if (reason.length == 0) {
                    revert ERC721InvalidReceiver(to);
                } else {
                    /// @solidity memory-safe-assembly
                    assembly {
                        revert(add(32, reason), mload(reason))
                    }
                }
            }
        }
    }

    // 实现 IERC721Metadata 接口的 tokenURI 函数, 用于查询 tokenId 的元数据链接 URI
    function tokenURI(
        uint256 tokenId
    ) public view virtual override returns (string memory) {
        require(_owners[tokenId] != address(0), "Token Not Exist");

        string memory baseURI = _baseURI();
        return
            bytes(baseURI).length > 0
                ? string(abi.encodePacked(baseURI, tokenId.toString()))
                : "";
    }

    // 计算 tokenURI 的基础 URI (就是把 baseURI 和 tokenId 拼接在一起), 通常需要开发者重写此函数以返回实际的基础 URI
    // demo - BAYC 的 baseURI 为 ipfs://QmeSjSinHpPnmXmspMjwiXyN6zS4E9zccariGR3jxcaWtq/
    function _baseURI() internal view virtual returns (string memory) {
        return "";
    }
}
```

<br><br>

# 写一个免费铸造的 APE

我们将利用 ERC721 标准编写一个免费铸造的 APE 合约, 总量设置为 10000. 只需重写 `mint` 和 `_baseURI` 函数.

```solidity
contract Ape is ERC721 {
    uint public MAX_APES = 10000;

    constructor(
        string memory name_,
        string memory symbol_
    ) ERC721(name_, symbol_) {}

    function _baseURI() internal pure override returns (string memory) {
        // 由于 baseURI 设置与 BAYC 相同, 元数据将直接获取无聊猿的, 类似 RRBAYC
        return "ipfs://QmeSjSinHpPnmXmspMjwiXyN6zS4E9zccariGR3jxcaWtq/";
    }

    function mint(address to, uint tokenId) external {
        require(tokenId >= 0 && tokenId < MAX_APES, "tokenId out of range");
        _mint(to, tokenId);
    }
}
```

<br><br>
