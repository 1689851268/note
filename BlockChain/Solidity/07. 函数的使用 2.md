# Modifier

函数装饰器 (Modifier) 可以在函数执行之前或之后插入代码逻辑，比如检查条件、限制访问权限等。

```solidity
contract Demo {
    address public owner;

    constructor() {
        owner = msg.sender;
    }

    // 定义装饰器
    modifier onlyOwner() {
        require(msg.sender == owner, "Not the contract owner");
        _;
    }

    // 使用装饰器
    function changeOwner(address newOwner) public onlyOwner {
        owner = newOwner;
    }
}
```

装饰器可以接收参数 & 函数可以同时使用多个装饰器：

```solidity
contract Demo {
    address public owner;

    constructor() {
        owner = msg.sender;
    }

    // 定义装饰器
    modifier onlyOwner() {
        require(msg.sender == owner, "Not the contract owner");
        _;
    }

    // 定义带参数的装饰器
    modifier validAddress(address _addr) {
        require(_addr != address(0), "Invalid address");
        // address(0) is often used to represent an invalid or uninitialized address.
        _;
    }

    // 同时使用多个装饰器
    function changeOwner(
        address newOwner
    ) public onlyOwner validAddress(newOwner) {
        owner = newOwner;
    }
}
```

三明治装饰器：可以在函数执行之前和之后插入代码逻辑的装饰器。demo - 防止重入攻击：

```solidity
contract Demo {
    bool private locked = false;

    // 定义一个防止重入攻击的装饰器
    modifier noReentrant() {
        require(!locked, "No Reentrant");
        locked = true;
        _;
        locked = false;
    }

    // 使用装饰器
    function withdraw() public noReentrant {
        // ... 提现逻辑 ...
    }
}
```

<br><br>

# 异常处理

Solidity 中有 3 个异常处理方法：

```solidity
contract Demo {
    // 1. require
    function testRequire(uint a) public pure returns (uint) {
        require(a > 10, "a must be greater than 10"); // 若条件不满足, 则抛出 `Error(string)`; 会退还剩余的 gas
        // 参数 2 可选;  参数 2 越长, 消耗的 gas 越多
        return a;
    }

    // 2. revert
    function testRevert(uint a) public pure returns (uint) {
        if (a <= 10) {
            revert("a must be greater than 10"); // 抛出 `Error(string)`, 会退还剩余的 gas
            // 参数 1 可选; 参数 1 越长, 消耗的 gas 越多
        }
        return a;
    }

    // 3. assert
    function testAssert(uint a) public pure returns (uint) {
        assert(a > 10); // 若条件不满足, 则抛出 `Panic(uint256)`; 会消耗剩余的 gas
        return a;
    }
}
```

自定义异常 - 可以自定义异常信息，且通常比字符串描述更节省 gas；搭配 revert 使用：

```solidity
contract Demo {
	// 声明自定义异常
    error MyError(address sender, uint value);

    function testCustomError(uint a) public view returns (uint) {
        if (a <= 10) {
            revert MyError(msg.sender, a); // 通过 revert 使用自定义异常
        }
        return a;
    }
}
```

try catch - 适用于处理外部函数调用或合约创建中的错误，支持 `Error` 和 `Panic` 错误：

```solidity
interface DataFeed {
    function getData(address token) external returns (uint value);
}

contract FeedConsumer {
    // 声明一个 DataFeed 类型的变量 feed
    DataFeed feed;
    // 声明一个用于记录错误次数的变量 errorCount
    uint errorCount;

    // 定义一个函数 rate, 接收一个地址类型的参数 token, 返回一个 uint 和一个 bool 类型的值
    function rate(address token) public returns (uint, bool) {
        // 使用 require 语句检查 errorCount 是否小于 10, 如果不满足条件则抛出错误并返回消息
        require(errorCount < 10, "Too many errors");

        // 使用 try/catch 语句尝试调用 feed.getData 函数
        try feed.getData(token) returns (uint v) {
            return (v, true);
        } catch Error(
            string memory reason // 捕获 Error 类型的错误, 并返回错误原因
        ) {
            errorCount++;
            return (0, false);
        } catch Panic(
            uint errorCode // 捕获 Panic 类型的错误, 并返回错误代码
        ) {
            errorCount++;
            return (0, false);
        } catch (
            bytes memory // 捕获其他类型的错误
        ) {
            errorCount++;
            return (0, false);
        }
    }
}
```

<br><br>

# Library

Library (库) 用于定义常用的功能函数，能在多个合约中复用。

1.  库不能定义状态变量

2.  库不能继承或被继承

3.  库不能接收以太币

4.  库不能被销毁

库函数的可见性设置为 public / external 时，调用会触发 delegatecall；设置为 internal 时不会触发；设置为 private 时，仅在库合约内部可见，其他合约无法调用。

2 种使用库函数的方式：

使用方式 1：

```solidity
library Math {
    function max(uint a, uint b) internal pure returns (uint) {
        return a >= b ? a : b;
    }
}

contract Test {
    function test() public pure returns (uint) {
        return Math.max(1, 2); // 通过库名 Math 调用里面的方法 max
    }
}
```

使用方式 2：

```solidity
library ArrayLib {
    function findIndex(
        uint[] memory arr,
        uint value
    ) internal pure returns (int) {
        for (uint i = 0; i < arr.length; i++) {
            if (arr[i] == value) {
                return int(i);
            }
        }
        return -1;
    }
}

contract Test {
    using ArrayLib for uint[]; // 通过库名 ArrayLib 增强类型 uint[]

    uint[] public arr = [1, 2, 3, 4, 5];

    function findIndex(uint value) public view returns (int) {
        return arr.findIndex(value); // uint[] 类型的变量可直接调用 ArrayLib 里面的方法 findIndex
    }
}
```

常用的库合约：

1.  [Strings](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/4a9cc8b4918ef3736229a5cc5a310bdc17bf759f/contracts/utils/Strings.sol)：提供操作字符串的函数，如将 uint 转换为字符串、将 uint 转换为十六进制字符串等

2.  [Address](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/4a9cc8b4918ef3736229a5cc5a310bdc17bf759f/contracts/utils/Address.sol)：提供地址相关的实用函数，如判断地址是否为合约、发送以太币等

3.  [Create2](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/4a9cc8b4918ef3736229a5cc5a310bdc17bf759f/contracts/utils/Create2.sol)：用于通过 CREATE2 操作码部署合约

4.  [Arrays](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/4a9cc8b4918ef3736229a5cc5a310bdc17bf759f/contracts/utils/Arrays.sol)：提供操作数组的函数，如查找、排序等

<br><br>

# import

import 语句用于在一个文件中引用另一个文件的内容：

```solidity
// 通过相对路径引入文件
import './Demo.sol';

// 通过网址引入文件
import 'https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/utils/Address.sol';

// 通过 NPM 包引入文件
import '@openzeppelin/contracts/utils/Address.sol';
```

```solidity
// 导入合约中指定的 "全局符号" (全局变量、函数、接口等)
import { A, B as C } from "xxx";
import * as D from "xxx";
```

<br><br>

# Gas Golf

Gas Golf 表示一些优化智能合约的手段，以减少 gas 消耗。

gas 是执行交易和智能合约所需的计算资源的度量单位。通过优化代码，可以显著降低交易成本，提高合约效率。

以下是一些常见的 gas 优化技巧：

1. **使用 calldata 代替 memory**

calldata 表示只读的数据位置，适用于函数参数。

```solidity
function example(uint[] calldata nums) external {
    // 使用 calldata 代替 memory
}
```

2. **使用前置递增运算符**

在循环中，使用 `++i` 代替 `i++` 可以节省 gas 。

```solidity
for (uint i = 0; i < nums.length; ++i) {
    // 使用 ++i 代替 i++
}
```

3. **短路求值**

在逻辑运算中，使用短路求值可以避免不必要的计算，从而节省 gas 。

```solidity
if (condition1 && condition2) {
    // 如果 condition1 为 false，则不会计算 condition2
}
```

4. **使用 unchecked 块**

Solidity 0.8.0 之后，会自动检查数学运算是否溢出。这提高了代码安全性，但也增加了 gas 消耗。如果你确定不会溢出且想节省 gas，可以使用 `unchecked` 跳过检查：

```solidity
function checkedAdd(uint8 a, uint8 b) public pure returns (uint8) {
    return a + b; // 如果 a + b 溢出, 会抛出异常并回滚
}

function uncheckedAdd(uint8 a, uint8 b) public pure returns (uint8) {
    unchecked {
        return a + b; // 如果 a + b 溢出, 会截断结果并返回
    }
}
```

5. **将常访问的值缓存到内存中**

```solidity
uint[] public nums;

function example() external view {
    uint[] memory _nums = nums; // 1️⃣ 缓存常访问的状态变量
    uint len = _nums.length; // 2️⃣ 缓存数组长度
    for (uint i = 0; i < nums.length; i++) {
        uint num = nums[i]; // 3️⃣ 存储尝访问的数组元素
    }
}
```

**demo**：

```solidity
// 未优化的代码
function sumIfEvenAndLessThan99(uint[] memory nums) external {
    for (uint i = 0; i < nums.length; i++) {
        bool isEven = nums[i] % 2 == 0;
        bool isLessThan99 = nums[i] < 99;
        if (isEven && isLessThan99) {
            total += nums[i];
        }
    }
}

// 优化后的代码
function sumIfEvenAndLessThan99(uint[] calldata nums) external {
    uint _total = total;
    uint len = nums.length;
    for (uint i = 0; i < len;) {
        uint num = nums[i];
        if (num % 2 == 0 && num < 99) {
            _total += num;
        }
        unchecked {
            ++i;
        }
    }
    total = _total;
}
```

<br><br>
