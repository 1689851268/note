# 荷式拍卖

荷式拍卖 (Dutch Auction) , 也称为减价拍卖.

拍卖从一个较高的起始价格开始, 这个价格通常高于市场预期. 随着时间的推移, 价格按照预先设定的降价阶梯逐步降低. 当价格降到某个竞买人愿意接受的水平时, 该竞买人出价并成交. 第一个应价的竞买人获胜, 并支付当时的价格.

在币圈, 很多 NFT 通过荷兰拍卖发售, 其中包括 Azuki 和 World of Women. 项目方非常喜欢这种拍卖形式, 主要有两个原因: ① 荷兰拍卖的价格由最高慢慢下降, 能让项目方获得最大的收入,② 拍卖持续较长时间 (通常 6 小时以上) , 可以避免 gas war .

代码基于 Azuki 的代码简化而成. DutchAuction 合约继承了 ERC721 和 Ownable 合约:

```solidity
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC721/ERC721.sol";

contract DutchAuction is ERC721, Ownable {
    constructor() Ownable(msg.sender) ERC721("DutchAuction", "DA") {}
}
```

合约中一共有 9 个状态变量, 其中有 6 个和拍卖相关:

```solidity
    uint256 public constant COLLECTION_SIZE = 10000; // NFT 总数
    uint256 public constant AUCTION_START_PRICE = 1 ether; // 起拍价
    uint256 public constant AUCTION_END_PRICE = 0.1 ether; // 最低价
    uint256 public constant AUCTION_TIME = 10 minutes; // 拍卖时间, 为了测试方便设为 10 分钟
    uint256 public constant AUCTION_DROP_INTERVAL = 1 minutes; // 每过多久时间, 价格衰减一次
    uint256 public constant AUCTION_DROP_PER_STEP =
        (AUCTION_START_PRICE - AUCTION_END_PRICE) /
            (AUCTION_TIME / AUCTION_DROP_INTERVAL); // 每次价格衰减步长

    uint256 public auctionStartTime; // 拍卖开始时间戳
    string private _baseTokenURI; // metadata URI
    uint256[] private _allTokens; // 记录所有存在的 tokenId
```

荷兰拍卖合约中共有 9 个函数, 下面仅介绍和拍卖相关的函数:

-   设定拍卖起始时间: 我们在构造函数中会声明当前区块时间为起始时间, 项目方也可以通过 setAuctionStartTime 函数来调整

```solidity
    constructor() Ownable(msg.sender) ERC721("DutchAuction", "DA") {
        auctionStartTime = block.timestamp;
    }

    function setAuctionStartTime(uint32 timestamp) external onlyOwner {
        auctionStartTime = timestamp;
    }
```

-   获取拍卖实时价格: getAuctionPrice 函数通过当前区块时间以及拍卖相关的状态变量来计算实时拍卖价格

```solidity
    function getAuctionPrice() public view returns (uint256) {
        if (block.timestamp < auctionStartTime) {
            // 当 block.timestamp 小于起始时间, 价格为最高价 AUCTION_START_PRICE
            return AUCTION_START_PRICE;
        } else if (block.timestamp - auctionStartTime >= AUCTION_TIME) {
            // 当 block.timestamp 大于结束时间, 价格为最低价 AUCTION_END_PRICE
            return AUCTION_END_PRICE;
        } else {
            // 当 block.timestamp 处于两者之间时, 则计算出当前的衰减价格
            uint256 steps = (block.timestamp - auctionStartTime) /
                AUCTION_DROP_INTERVAL;
            return AUCTION_START_PRICE - (steps * AUCTION_DROP_PER_STEP);
        }
    }
```

-   用户拍卖并铸造 NFT: 用户通过调用 auctionMint 函数, 支付 ETH 参加荷兰拍卖并铸造 NFT

```solidity
    function auctionMint(uint256 quantity) external payable {
        // 检查是否设置起拍时间, 拍卖是否开始
        require(
            auctionStartTime != 0 && block.timestamp >= auctionStartTime,
            "sale has not started yet"
        );

        // 检查是否超过 NFT 上限
        require(
            totalSupply() + quantity <= COLLECTION_SIZE,
            "not enough remaining reserved for auction to support desired mint amount"
        );

        // 计算 mint 成本
        uint256 totalCost = getAuctionPrice() * quantity;

        // 检查用户是否支付足够 ETH
        require(msg.value >= totalCost, "Need to send more ETH.");

        // 铸造 NFT
        for (uint256 i = 0; i < quantity; i++) {
            uint256 mintIndex = totalSupply();
            _mint(msg.sender, mintIndex);
            _addTokenToAllTokensEnumeration(mintIndex);
        }

        // 多余 ETH 退款
        if (msg.value > totalCost) {
            payable(msg.sender).transfer(msg.value - totalCost); // 注意一下这里是否有重入的风险
        }
    }
```

-   项目方取出筹集的 ETH: 项目方可以通过 withdrawMoney 函数提走拍卖筹集的 ETH

```solidity
    function withdrawMoney() external onlyOwner {
        (bool success, ) = msg.sender.call{value: address(this).balance}("");
        require(success, "Transfer failed.");
    }
```

-   其他函数:

```solidity
    // ERC721Enumerable 中 totalSupply 函数的实现
    function totalSupply() public view virtual returns (uint256) {
        return _allTokens.length;
    }

    // 在 _allTokens 中添加一个新的 token
    function _addTokenToAllTokensEnumeration(uint256 tokenId) private {
        _allTokens.push(tokenId);
    }

    // BaseURI
    function _baseURI() internal view virtual override returns (string memory) {
        return _baseTokenURI;
    }

    // set BaseURI
    function setBaseURI(string calldata baseURI) external onlyOwner {
        _baseTokenURI = baseURI;
    }
```

<br><br>

# 英式拍卖

英式拍卖 (English Auction) , 也称为增价拍卖.

拍卖从一个较低的起拍价开始. 竞买人按竞价阶梯逐步提高出价. 每次出价必须高于当前最高出价. 当没有人愿意再出更高的价格时, 拍卖结束, 出价最高者成为买受人.

1. 定义接口:

```solidity
interface IERC721 {
    function transferFrom(address _from, address _to, uint _tokenId) external;
}
```

2. 定义事件:

```solidity
contract EnglishAuction {
    // 当拍卖开始时触发
    event Start();

    // 当有新的出价时触发
    event Bid(address indexed bidder, uint amount);

    // 当退款时触发
    event Refund(address indexed bidder, uint amount);

    // 当拍卖结束时触发
    event End(address highestBidder, uint highestBid);
}
```

3. 定义状态变量:

```solidity
    // 拍卖的 NFT
    IERC721 public immutable nft;

    // 拍卖的 NFT ID
    uint public immutable nftId;

    // 卖家地址
    address payable public immutable seller;

    // 拍卖结束时间
    uint public endTime;

    // 拍卖是否已开始
    bool public isStarted;

    // 拍卖是否已结束
    bool public isEnded;

    // 当前最高出价者
    address public highestBidder;

    // 当前最高出价
    uint public highestBid;

    // 出价记录
    mapping(address => uint) public bids;
```

4. 实现构造函数:

```solidity
    constructor(address _nft, uint _nftId, uint _startBid) {
        nft = IERC721(_nft);
        nftId = _nftId;
        seller = payable(msg.sender);
        highestBid = _startBid;
    }
```

5. 实现函数开始拍卖:

```solidity
    function startAuction() external {
        require(msg.sender == seller, "Only seller can start auction");
        require(!isStarted, "Auction already started");

        isStarted = true;
        endTime = block.timestamp + 7 days;
        // 将 nftId 代币从卖家转给合约
        nft.transferFrom(seller, address(this), nftId);

        emit Start();
    }
```

6. 实现函数出价:

```solidity
    function bid() external payable {
        require(isStarted, "Auction not started");
        require(block.timestamp < endTime && !isEnded, "Auction ended");
        require(msg.sender != seller, "Seller cannot bid");
        require(msg.value > highestBid, "Bid must be higher than highest bid");

        if (highestBidder != address(0)) {
            // 记录上一个最高出价者及其出价, 以便退款
            bids[highestBidder] += highestBid;
        }
        highestBidder = msg.sender;
        highestBid = msg.value;

        emit Bid(msg.sender, msg.value);
    }
```

7. 实现函数结束拍卖:

```solidity
    function endAuction() external {
        require(isStarted, "Auction not started");
        require(!isEnded, "Auction already ended");
        require(block.timestamp >= endTime, "Auction not ended");

        isEnded = true;
        if (highestBidder != address(0)) {
            // 如果有人出价
            // 将 nft 从合约转给最高出价者
            nft.transferFrom(address(this), highestBidder, nftId);
            // 将最高出价转给卖家
            seller.transfer(highestBid);
        } else {
            // 无人出价
            // 将 ntf 从合约转回给卖家
            nft.transferFrom(address(this), seller, nftId);
        }

        emit End(highestBidder, highestBid);
    }
```

8. 实现函数退款:

```solidity
    function refund() external {
        require(msg.sender != highestBidder, "Highest bidder cannot refund");

        uint amount = bids[msg.sender];
        bids[msg.sender] = 0;
        payable(msg.sender).transfer(amount);

        emit Refund(msg.sender, amount);
    }
```

1. 部署 MyNFT 合约 (在笔记 "代币.md" 中)

2. 传入编辑器地址,tokenID (这里以 123 为例), 调用 MyNFT 合约的 mint 方法, 为编辑器地址 mint 一个 NFT

3. 传入 tokenID (123), 调用 MyNFT 合约的 ownerOf 方法, 查看该 NFT 的所有者是否为编辑器地址

4. 传入 MyNFT 合约地址,tokenID (123),起拍价 (1000000), 部署 EnglishAuction 合约

5. 传入 EnglishAuction 合约地址,tokenID (123), 调用 MyNFT 合约的 approve 方法, 授权 EnglishAuction 合约管理该 NFT

模拟卖家开始拍卖:

1. 调用 EnglishAuction 合约的 startAuction 方法, 开始拍卖

模拟用户出价:

1. 切换编辑器地址 2, 设置以太数 (1000001), 调用 EnglishAuction 合约的 bid 方法, 出价

2. 切换编辑器地址 3, 设置以太数 (1000002), 调用 EnglishAuction 合约的 bid 方法, 出价

模拟卖家结束拍卖:

1. 待拍卖时间结束, 调用 EnglishAuction 合约的 endAuction 方法, 结束拍卖

2. 传入 tokenID (123), 调用 MyNFT 合约的 ownerOf 方法, 查看该 NFT 的所有者是否为当前编辑器地址 3

模拟用户退款:

1. 切换编辑器地址 2, 调用 EnglishAuction 合约的 refund 方法, 退款

<br><br>
