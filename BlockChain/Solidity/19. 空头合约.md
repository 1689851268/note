# Airdrop

Airdrop (空投) 是币圈中一种营销策略, 项目方将代币免费发放给特定用户群体. 为了拿到空投资格, 用户通常需要完成一些简单的任务, 如测试产品,分享新闻,介绍朋友等. 项目方通过空投可以获得种子用户, 而用户可以获得一笔财富, 两全其美.

因为每次接收空投的用户很多, 项目方不可能一笔一笔的转账. 利用智能合约批量发放 ERC20 代币, 可以显著提高空投效率.

```solidity
contract Airdrop {
    mapping(address => uint) failTransferList; // 失败转账列表

    // 返回 uint 数组的和
    function getSum(uint256[] calldata _arr) public pure returns (uint sum) {
        for (uint i = 0; i < _arr.length; i++) sum = sum + _arr[i];
    }
}
```

Airdrop 合约逻辑非常简单: 利用循环将 ERC20 代币发送给多个地址.

合约中包含 2 个核心函数:

-   multiTransferToken 函数: 批量发送 ERC20 代币; 包含 3 个参数: ① 代币合约地址,② 接收空投的用户地址数组,③ 空投数量数组

```solidity
    function multiTransferToken(
        address _token,
        address[] calldata _addresses,
        uint256[] calldata _amounts
    ) external {
        // 检查: _addresses 的长度 == _amounts 的长度
        require(
            _addresses.length == _amounts.length,
            "Lengths of Addresses and Amounts NOT EQUAL"
        );

        IERC20 token = IERC20(_token); // 声明 ERC20 合约变量
        uint _amountSum = getSum(_amounts); // 计算空投代币总量
        // 检查: 授权代币数量 >= 空投代币总量
        require(
            token.allowance(msg.sender, address(this)) >= _amountSum,
            "Need Approve ERC20 token"
        );

        // for 循环, 利用 transferFrom 函数发送空投
        for (uint8 i; i < _addresses.length; i++) {
            token.transferFrom(msg.sender, _addresses[i], _amounts[i]);
        }
    }
```

-   multiTransferETH 函数: 批量发送 ETH; 包含 2 个参数: ① 接收空投的用户地址数组,② 空投数量数组.

```solidity
    function multiTransferETH(
        address payable[] calldata _addresses,
        uint256[] calldata _amounts
    ) public payable {
        // 检查: _addresses 的长度 == _amounts 的长度
        require(
            _addresses.length == _amounts.length,
            "Lengths of Addresses and Amounts NOT EQUAL"
        );

        uint _amountSum = getSum(_amounts); // 计算空投 ETH 总量
        // 检查: msg.value == 空投 ETH 总量
        require(msg.value == _amountSum, "Transfer amount error");

        // for 循环, 发送 ETH
        for (uint256 i = 0; i < _addresses.length; i++) {
            (bool success, ) = _addresses[i].call{value: _amounts[i]}("");
            if (!success) {
                failTransferList[_addresses[i]] = _amounts[i];
            }
        }
    }
```

<br><br>
