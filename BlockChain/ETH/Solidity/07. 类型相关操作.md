# 操作 Enum

```solidity
contract Demo {
    enum State {
        PENDING,
        ACTIVE,
        INACTIVE
    }
    State private state;

    // 更新 state
    function updateState(State _state) public {
        state = _state;
    }

    // 重置 state
    function resetState() public {
        delete state; // 使用 delete 重置枚举变量为默认值 `PENDING`, 即 `0`
    }

    // 获取 state
    function getState() public view returns (State) {
        return state;
    }
}
```

1. 部署 Demo 合约, state 默认值为 PENDING, 即 0

2. 调用 getState 方法, 返回值为 uint8 类型的整数 0

3. 调用 updateState 方法, 传入 0 / 1 / 2, 分别表示 PENDING / ACTIVE / INACTIVE

4. 调用 getState 方法, 返回值为传入的整数

5. 调用 resetState 方法, 重置 state 为默认值 PENDING, 即 0

6. 调用 getState 方法, 返回值为 0

<br>

**获取枚举类型的最大最小值**: 可通过 `type(NameOfEnum).max` 和 `type(NameOfEnum).min` 获取

<br><br>

# 操作 Array

**操作动态大小的数组**:

```solidity
contract Demo {
    uint[] private dynamicArray;

    // 获取数组长度
    function getLength() public view returns (uint) {
        return dynamicArray.length;
    }

    // 获取指定元素
    function get(uint _index) public view returns (uint) {
        return dynamicArray[_index];
    }

    // 更新指定元素
    function set(uint _index, uint _value) public {
        dynamicArray[_index] = _value;
    }

    // 添加元素
    function push(uint _value) public {
        dynamicArray.push(_value); // 使用 push 添加元素到数组末尾; 不传参则添加默认值 `0`
    }

    // 重置指定元素; eg: [1, 2, 3] - [1, 0, 3]
    function deleteByIndex(uint _index) public {
        delete dynamicArray[_index]; // 使用 delete 重置该元素为默认值 `0`
    }

    // 删除元素; eg: [1, 2, 3] - [1, 2]
    function pop() public {
        dynamicArray.pop(); // 使用 pop 删除数组末尾的元素
    }

    // 删除指定元素; eg: [1, 2, 3] - [1, 3]
    function remove(uint _index) public {
        for (uint i = _index; i < dynamicArray.length - 1; i++) {
            dynamicArray[i] = dynamicArray[i + 1];
        }
        dynamicArray.pop();
    }

    // 删除所有元素; eg: [1, 2, 3] - []
    function clear() public {
        delete dynamicArray; // 使用 delete 重置动态大小数组为默认值 `[]`
    }
}
```

notes: 仅 storage 的动态数组才能使用 `push` 和 `pop` 方法, memory 和 calldata 类型的数组不支持.

**操作固定大小的数组**:

```solidity
contract Demo {
    uint[3] private fixedArray1;
    uint[] private fixedArray2 = new uint[](3);

    // 获取数组长度
    function getLength() public view returns (uint, uint) {
        return (fixedArray1.length, fixedArray2.length);
    }

    // 获取指定元素
    function get(uint _index) public view returns (uint, uint) {
        return (fixedArray1[_index], fixedArray2[_index]);
    }

    // 更新指定元素
    function set(uint _index, uint _value) public {
        fixedArray1[_index] = _value;
        fixedArray2[_index] = _value;
    }

    // 重置指定元素; eg: [1, 2, 3] - [1, 0, 3]
    function deleteByIndex(uint _index) public {
        delete fixedArray1[_index];
        delete fixedArray2[_index];
        // 使用 delete 重置该元素为默认值 `0`
    }

    // 重置所有元素; eg: [1, 2, 3] - [0, 0, 0]
    function clear() public {
        delete fixedArray1;
        delete fixedArray2;
        // 使用 delete 重置固定大小数组为默认值 `[0, 0, 0]`
    }
}
```

**在内存中创建数组**:

```solidity
contract Demo {
    function createArray(uint size) public pure returns (uint[] memory) {
        uint[] memory arr = new uint[](size);
        for (uint i = 0; i < size; i++) {
            arr[i] = i;
        }
        return arr;
    }
}
```

关于数组字面量 (eg `[1, 2, 3]`), 默认会使用存储空间最小的类型 (即 `uint8`), 若想定义为其他类型 (eg `uint`), 需要至少显式转换一个元素 (eg `[uint(1), 2, 3]`).

**数组切片**:

语法: `array[start:end]`, 其中 `start` 为起始索引, `end` 为结束索引 (不包含). 若不指定 `start`, 则默认为 0; 若不指定 `end`, 则默认为数组长度.

```solidity
function sliceFourBytes(string calldata payload) public pure {
    string memory leading4Bytes = payload[:4];
    console.log("leading 4 bytes: %s", leading4Bytes);
}
```

目前在 Solidity 中, 数组切片只能对 `calldata` 进行操作, 不能用于 `memory` 和 `storage`. 并且由于 `calldata` 数据不可更改, 无法对数组切片的值进行修改.

**多维数组**:

-   静态多维数组声明:

若 `T` 为类型, 声明格式为 `T[col][row] DataLocation arrName;`, 其中 `row` 和 `col` 分别定义行数和列数, `DataLocation` 是数据位置, `arrName` 是数组名. 例如, `uint[3][5]` 是 5 行 3 列的多维数组. Solidity 中声明多维数组的行列顺序与 C,JavaScript 等语言相反, 且需从右往左解读, 如 `uint[2][3][4] `包含 `4` 个 `uint[2][3]` 静态数组.

静态多维数组大小必须在编译时确定, 不能用变量定义, 如 `uint size = 2; uint[size][size] memory arr;` 是非法的.

-   动态多维数组声明:

若 `T` 为类型, 声明格式为 `T[][] DataLocation arrName;`, `DataLocation` 指定存储位置, `arrName` 是数组名. 例如: `uint[][] memory arrMem; uint[][3] memory arrMem2; uint[3][] storage arrStorage;`

-   静态多维数组初始化:

```solidity
uint[3][2] arr = [[1, 2, 3], [4, 5, 6]];
```

-   动态多维数组初始化:

```solidity
uint[][] arr = [[1, 2, 3], [4, 5, 6]];
```

```solidity
uint[][] arr;

function initArray() public {
    uint row = 3;
    uint col = 2;
    arr = new uint[][](row);
    for (uint i = 0; i < row; i++) {
        arr[i] = new uint[](col);
        for (uint j = 0; j < col; j++) {
            arr[i][j] = i * col + j;
        }
    }
}
```

<br><br>

# 操作 Mapping

```solidity
contract Demo {
    mapping(address => uint) private myBalance;

    // 获取指定值
    function getMyBalance(address _user) public view returns (uint) {
        return myBalance[_user];
    }

    // 更新指定值
    function setMyBalance(address _user, uint _amount) public {
        myBalance[_user] += _amount;
    }

    // 重置指定值
    function deleteMyBalance(address _user) public {
        delete myBalance[_user]; // 使用 delete 重置该值为默认值 `0`
    }
}
```

映射类型不能直接遍历, 因为不存储键的列表或顺序。若要实现遍历或统计元素数量, 通常需在映射外维护一个数组来记录键:

```solidity
contract Demo {
    mapping(address => uint) private balance; // 用户 => 余额
    mapping(address => bool) public isCustomer; // 用户 => 是否是客户
    address[] public customers; // 客户列表

    // 存款
    function deposit(address _customer, uint _amount) public {
        balance[_customer] += _amount;
        if (!isCustomer[_customer]) {
            customers.push(_customer);
            isCustomer[_customer] = true;
        }
    }

    // 通过地址取款
    function getBalance(address _customer) public view returns (uint) {
        return balance[_customer];
    }

    // 通过索引取款
    function getBalanceByIndex(uint _index) public view returns (uint) {
        return balance[customers[_index]];
    }
}
```

<br><br>

# 操作 Struct

```solidity
contract Demo {
    struct Student {
        uint id;
        string name;
    }
    Student[] private students;
}
```

1.  创建 Struct 实例:

```solidity
function addStudent1(string memory _name) public {
    // 按属性顺序创建 Struct 实例
    Student memory student = Student(students.length, _name);
    // 添加到 students 数组
    students.push(student);
}

function addStudent2(string memory _name) public {
    // 按属性名创建 Struct 实例
    Student memory student = Student({id: students.length, name: _name});
    // 添加到 students 数组
    students.push(student);
}

function addStudent3(string memory _name) public {
    // 创建 Struct 实例, 但不初始化, 会使用默认值
    Student memory student;
    // 设置属性值
    student.id = students.length;
    student.name = _name;
    // 添加到 students 数组
    students.push(student);
}
```

2.  访问 Struct 实例属性:

```solidity
function getStudentInfoByIndex(
    uint _index
) public view returns (uint, string memory) {
    Student memory student = students[_index];
    return (student.id, student.name);
}
```

3.  更新 Struct 实例属性:

```solidity
function updateStudentNameByIndex(uint _index, string memory _name) public {
    Student storage student = students[_index];
    student.name = _name;
}
```

4.  重置 Struct 实例属性:

```solidity
function deleteStudentNameByIndex(uint _index) public {
    Student storage student = students[_index];
    delete student.name; // 使用 delete 重置该属性值为默认值 `""`
}
```

5.  重置 Struct 实例:

```solidity
function deleteStudentByIndex(uint _index) public {
    delete students[_index]; // 使用 delete 重置该实例为默认值 `{ name: "", id: 0 }`
}
```

结构体不能直接含自身类型成员, 但可通过映射间接包含:

```solidity
contract Demo {
    struct Node {
        uint id;
        string name;
        mapping(uint => Node) children;
    }
}
```

对于含 mapping 的结构体, 需要这般初始化:

```solidity
contract Demo {
    struct Person {
        string name;
        uint256 age;
        mapping(string => uint256) data;
    }

    Person person;

    function initPerson() public {
        person.name = "Alice";
        person.age = 30;
        person.data["balance"] = 100;
    }
}
```

<br><br>
