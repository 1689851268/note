# EIP 与 ERC

EIP 全称 Ethereum Improvement Proposals (以太坊改进建议), 是以太坊开发者社区提出的改进建议, 是一系列以编号排定的文件. EIP 可以是 Ethereum 生态中任意领域的改进, 比如新特性,ERC,协议改进,编程工具等.

ERC 全称 Ethereum Request For Comment (以太坊意见征求稿), 用以记录以太坊上应用级的各种开发标准和协议. 如典型的 Token 标准 (ERC20,ERC721),名字注册 (ERC26,ERC13),URI 范式 (ERC67),Library/Package 格式 (EIP82),钱包格式 (EIP75,EIP85) .

ERC 协议标准是影响以太坊发展的重要因素, 像 ERC20,ERC223,ERC721,ERC777 等, 都对以太坊生态产生了很大影响.

所以最终结论: EIP 包含 ERC .

<br><br>

# ERC20

ERC20 全称 "Ethereum Request for Comment 20", 是以太坊上的代币标准. ERC20 标准定义了一组函数和事件, 使得代币可以在不同的应用和平台之间相互操作.

```solidity
interface IERC20 {
    // 查询代币的总供应量
    function totalSupply() external view returns (uint);

    // 查询 account 的代币余额
    function balanceOf(address account) external view returns (uint);

    // 查询 owner 授权给 spender 的代币数量
    function allowance(
        address owner,
        address spender
    ) external view returns (uint);

    // 从调用者转移 amount 数量的代币到 to
    function transfer(address to, uint amount) external returns (bool);

    // 授权 spender 可以转移调用者 amount 数量的代币
    function approve(address spender, uint amount) external returns (bool);

    // 调用者从 from 转移 amount 数量的代币到 to, 需要先通过 approve 授权
    function transferFrom(
        address from,
        address to,
        uint amount
    ) external returns (bool);
}
```

```solidity
    // 在转移代币时触发
    event Transfer(address indexed from, address indexed to, uint value);

    // 在调用 approve 时触发
    event Approval(address indexed owner, address indexed spender, uint value);
```

以下是一个简单的 ERC20 代币合约示例:

```solidity
contract ERC20 is IERC20 {
    // 存储代币的名称,符号和小数位数
    string public name = "TestToken";
    string public symbol = "TTK";
    uint8 public decimals = 18;

    // 存储代币的总供应量
    uint public totalSupply;

    // 存储每个地址的代币余额;  owner => balance
    mapping(address => uint) public balanceOf;

    // 存储每个地址对其他地址的授权额度;  owner => spender => amount
    mapping(address => mapping(address => uint)) public allowance;

    // 从调用者转移 amount 数量的代币到 to
    function transfer(
        address to,
        uint amount
    ) external override returns (bool) {
        balanceOf[msg.sender] -= amount;
        balanceOf[to] += amount;
        emit Transfer(msg.sender, to, amount);
        return true;
    }

    // 授权 spender 可以转移调用者 amount 数量的代币
    function approve(
        address spender,
        uint amount
    ) external override returns (bool) {
        allowance[msg.sender][spender] = amount;
        emit Approval(msg.sender, spender, amount);
        return true;
    }

    // 调用者从 from 转移 amount 数量的代币到 to, 需要先通过 approve 授权
    function transferFrom(
        address from,
        address to,
        uint amount
    ) external override returns (bool) {
        balanceOf[to] += amount;
        balanceOf[sender] -= amount;
        allowance[sender][msg.sender] -= amount;
        emit Transfer(sender, to, amount);
        return true;
    }
}
```

除了上述核心方法, 你可能还听过 `mint` & `burn`. 它们通常用于 ERC20 代币合约中, 以增加或减少代币的总供应量.

```solidity
    // 为调用者创建 amount 数量的代币
    function mint(uint amount) external {
        totalSupply += amount;
        balanceOf[msg.sender] += amount;
        emit Transfer(address(0), msg.sender, amount);
    }

    // 为调用者销毁 amount 数量的代币
    function burn(uint amount) external {
        totalSupply -= amount;
        balanceOf[msg.sender] -= amount;
        emit Transfer(msg.sender, address(0), amount);
    }
```

OpenZeppelin 提供了安全且经过审计的 ERC20 实现, 使用 OpenZeppelin 库可以简化 ERC20 代币的实现:

```solidity
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract MyERC20Token is ERC20 {
    constructor(
        string memory _name,
        string memory _symbol,
        uint256 _totalSupply
    ) ERC20(_name, _symbol) {
        _mint(msg.sender, _totalSupply);
    }
}
```

<br><br>

# WETH

WETH (Wrapped Ether) 是以太坊 (ETH) 的包装版本, 它遵循 ERC20 代币标准. 由于 ETH 本身并不符合 ERC20 标准, 因此在某些去中心化应用 (DApp) 和去中心化金融 (DeFi) 平台上使用时会有一些限制. WETH 的出现解决了这个问题, 使 ETH 可以在这些平台上无缝使用.

```solidity
contract WETH {
    // 代币名称,符号,小数位数
    string public name = "Wrapped Ether";
    string public symbol = "WETH";
    uint8 public decimals = 18;

    // 记录每个地址的 WETH 余额
    mapping(address => uint) public balanceOf;

    // 记录存入 ETH 的事件
    event Deposit(address indexed account, uint amount);

    // 记录提取 ETH 的事件
    event Withdrawal(address indexed account, uint amount);

    // 使合约可以接受 ETH
    receive() external payable {
        deposit();
    }

    // 接受 ETH 并将其转换为 WETH
    function deposit() public payable {
        balanceOf[msg.sender] += msg.value;
        emit Deposit(msg.sender, msg.value);
    }

    // 将 WETH 转换回 ETH 并提取到调用者的地址
    function withdraw(uint amount) public {
        require(balanceOf[msg.sender] >= amount, "Insufficient balance");
        balanceOf[msg.sender] -= amount;
        payable(msg.sender).transfer(amount);
        emit Withdrawal(msg.sender, amount);
    }

    // 返回合约中存储的 ETH 总量
    function totalSupply() public view returns (uint) {
        return address(this).balance;
    }
}
```

我们可以直接使用 OpenZeppelin 的 ERC20 合约库来实现 WETH 合约:

```solidity
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract WETH is ERC20 {
    // 记录存入 ETH 的事件
    event Deposit(address indexed account, uint amount);

    // 记录提取 ETH 的事件
    event Withdrawal(address indexed account, uint amount);

    // 初始化 WETH 合约
    constructor() ERC20("Wrapped Ether", "WETH") {}

    // 使合约可以接受 ETH
    receive() external payable {
        deposit();
    }

    // 接受 ETH 并将其转换为 WETH
    function deposit() public payable {
        _mint(msg.sender, msg.value);
        emit Deposit(msg.sender, msg.value);
    }

    // 将 WETH 转换回 ETH 并提取到调用者的地址
    function withdraw(uint amount) public {
        _burn(msg.sender, amount);
        payable(msg.sender).transfer(amount);
        emit Withdrawal(msg.sender, amount);
    }
}
```

1. 部署 WETH 合约

2. 调用 WETH 合约的 deposit 函数, 设置传入的 ETH 数量, 这里以 1 ETH 为例

3. 调用 WETH 合约继承的 balanceOf 函数, 传入部署 WETH 合约的地址, 查看 WETH 余额

4. 调用 WETH 合约继承的 totalSupply 函数, 查看合约中存储的 ETH 总量

5. 调用 WETH 合约的 withdraw 函数, 传入提取的 WETH 数量, 提取 ETH

6. 调用 WETH 合约继承的 balanceOf 函数, 查看 WETH 余额

7. 调用 WETH 合约继承的 totalSupply 函数, 查看合约中存储的 ETH 总量

<br><br>
