# Value Type

Value Type (值类型) 的变量在赋值或作为函数参数传递时, 会复制其数据的值.

<br>

## Boolean

```solidity
contract Demo {
    bool public isActive; // 默认为 false
}
```

布尔类型的值可以进行以下运算:

-   逻辑运算: `!` (逻辑非), `&&` (逻辑与), `||` (逻辑或)

-   比较运算: `==` (等于), `!=` (不等于)

其中, `&&` 和 `||` 是短路运算符, 即如果第一个操作数已经能够确定整个表达式的值, 则不会再计算第二个操作数.

<br>

## Integer

-   无符号整数类型: `uint8`, `uint16`, ..., `uint256` (`uint256` 可简写为 `uint`)

-   有符号整数类型: `int8`, `int16`, ..., `int256` (`int256` 可简写为 `int`)

```solidity
contract Demo {
    // 有符号整数类型
    int public signedInt; // 默认为 0, 取值范围为 -2^255 ~ 2^255-1
    // 无符号整数类型
    uint public unsignedInt; // 默认为 0, 取值范围为 0 ~ 2^256-1
}
```

整数类型的值可以进行以下运算:

-   算术运算: `+` (加), `-` (减), `*` (乘), `/` (除), `%` (取余), `**` (幂)

-   比较运算: `==` (等于), `!=` (不等于), `<` (小于), `>` (大于), `<=` (小于等于), `>=` (大于等于)

-   位运算: `&` (按位与), `|` (按位或), `^` (按位异或), `~` (按位取反), `<<` (左移), `>>` (右移)

<br>

## Address

-   20 字节 (160 位) 大小, 用于存储合约或账户的地址

```solidity
contract Demo {
    address public owner; // 默认为 0x0000000000000000000000000000000000000000,  即 address(0)
}
```

地址类型的值可以进行比较运算: `==` (等于), `!=` (不等于), `<` (小于), `>` (大于), `<=` (小于等于), `>=` (大于等于), 通常用来判断两个地址是否相等.

<br>

## Fixed-length Byte

-   `bytes1`, `bytes2`, ..., `bytes32`

```solidity
contract Demo {
    bytes1 public dataHash1; // 默认为 0x00
    bytes2 public dataHash2; // 默认为 0x0000
}
```

可以看出, 1 字节 (byte) - 8 位 (bit) - 2 个十六进制数. 因为 `2^4 = 16`, 即 1 个十六进制数可以表示 4 位二进制数.

定长字节数组类型的值可以进行以下运算:

-   比较运算: `==` (等于), `!=` (不等于), `<` (小于), `>` (大于), `<=` (小于等于), `>=` (大于等于)

-   位运算: `&` (按位与), `|` (按位或), `^` (按位异或), `~` (按位取反), `<<` (左移), `>>` (右移)

定长字节数组类型的变量可以通过下标访问其元素. eg: `dataHash1[0]`, `dataHash2[1]`. 注意避免越界访问.

<br>

## Enum

-   枚举类型是特殊的整型, 其内部将名称与从 `0` (uint8) 开始的正整数对应

-   枚举的默认值是其定义的第一个值

```solidity
contract Demo {
    enum State {
        PENDING,  // 0
        ACTIVE,  // 1
        INACTIVE // 2
    }
    State public state; // 默认值为 PENDING,  即 0
}
```

enum 可以在合约外部定义, 并在其他合约中用 `import` 引入.

<br><br>

# Reference Type

Reference Type (引用类型) 的变量在赋值或作为函数参数传递时, 会复制存储数据的存储地址.

<br>

## String

```solidity
contract Demo {
    string public stringData; // 默认为空字符串 ""
}
```

在 Solidity 中, string 不能进行下标访问和获取长度操作. 可将 string 转换成 bytes 后再进行下标访问和获取长度.

<br>

## Dynamic-length Byte

```solidity
contract Demo {
    bytes public byteData; // 默认为 0x
}
```

`bytes` 类似于 `bytes1[]` 数组, 但在内存和调用数据中的存储更紧凑. `bytes1[]` 数组每个元素需占 32 字节或其倍数 (不足 32 字节会自动填充), `bytes` 不存在此填充要求.若字节数组长度不变且不超 32 字节, 推荐用静态字节数组, 效率高且省 Gas; 需灵活调整大小则用动态字节数组.

`string` 内部结构与 `bytes` 基本相同, 但不支持下标访问和长度查询, 适用于不同用途, 二者在存储大量数据时都有较高存储效率. 可用 `bytes(str)` 将 `string` 转换为 `bytes`, 用 `string(b)` 将 `bytes` 转换为 `string`.

<br>

## Array

-   固定大小数组类型: `uint[5]` 表示包含 5 个无符号整数的数组

-   动态大小数组类型: `uint[]` 表示大小可变的无符号整数数组

```solidity
contract Demo {
    // 固定大小数组类型
    uint[3] public fixedArray1; // 默认为 [0,  0,  0]
    // 动态大小数组类型
    uint[] public dynamicArray; // 默认为 []
}
```

```solidity
contract Demo {
    // 初始化固定大小数组
    uint[3] public fixedArrayInit1 = [1, 2]; // [1,  2,  0]

    // 初始化动态大小数组
    // 方式 1: (在函数外可用, 在函数内不可用)
    uint[] public dynamicArrayInit1 = [1, 2]; // [1,  2]
    // 方式 2: (在函数内外都可用)
    uint[] public dynamicArrayInit2 = new uint[](3); // [0,  0,  0]
}
```

<br>

## Structure

用于将不同类型的数据组合在一起

```solidity
contract Demo {
    struct Person {
        string name;
        uint age;
    }
    Person public person;
    // 数据的默认值为对应类型的默认值,  这里 name 默认为空字符串 "",  age 默认为 0
}
```

struct 可以在合约外部定义, 并在其他合约中用 `import` 引入.

<br><br>

# Mapping Type

Mapping Type (映射类型) 是存储键值对的数据结构, 可以理解为哈希表.

```solidity
contract Demo {
    mapping(address => uint) public balances;
    // key-value 中,  value 的默认值为对应类型的默认值,  这里为 uint 的默认值 0
}
```

需要注意: ① 映射的 key 只能是 Solidity 内置的值类型, 如 uint, address; ② 映射类型也属于引用类型的一种; ③ 映射的存储位置必需是 storage. ④ 当函数可见性为 public 或 external 时, 不能在函数的参数或返回值中使用映射类型

原理: 映射用 `keccak256(abi.encodePacked(key, slot))` 作 offset 存取 value, 其中 slot 是映射变量定义所在的插槽位置.

<br><br>

# Function Type

Function Type (函数类型) 是 Solidity 的一种特殊类型, 用于存储函数的引用.

```solidity
contract Demo {
    function add(uint a,  uint b) public pure returns (uint) {
        return a + b;
    }
}
```

<br><br>

# 字符串 & 字节

字符串字面值可隐式转换为字节类型:

```solidity
contract Demo {
    bytes32 public _byte32 = "MiniSolidity";
    // 以字节形式存储字符串 "MiniSolidity"
    // 0x4d696e69536f6c69646974790000000000000000000000000000000000000000

    bytes1 public _byte = _byte32[0]; // 取出第一个字节
    // 0x4d
}
```

```solidity
contract Demo {
    bytes public _byte32 = "MiniSolidity";
    // 以字节形式存储字符串 "MiniSolidity"
    // 0x4d696e69536f6c6964697479

    bytes1 public _byte = _byte32[0]; // 取出第一个字节
    // 0x4d
}
```

<br><br>

# 整数 & 枚举

枚举可以显式地和 uint 相互转换, 且会检查转换的正整数是否在枚举的长度内:

```solidity
contract Demo {
    enum State {
        PENDING,  // 0
        ACTIVE,  // 1
        INACTIVE // 2
    }
    State public state = State.ACTIVE; // 1

    uint public stateValue = uint(state); // 1
    State public newState = State(stateValue); // ACTIVE,  即 1
}
```

<br><br>

# 类型转换

可以使用构造函数将一个类型转换为另一个类型:

```solidity
contract Demo {
    // 情况 1: 小范围类型转换为大范围类型, 会隐式转换
    uint8 public a = 255;
    uint public b = a; // 255

    // 情况 2: 大范围类型转换为小范围类型, 需要显式转换; 会截断高位
    uint public c = 1000;
    uint8 public d = uint8(c); // 232
}
```

<br><br>

# 自定义值类型

自定义值类型 (User Defined Value Types) 通过 `type C is V` 来定义, 其中 `C` 是新定义的类型, `V` 必须是 Solidity 的原生类型, 如 `type Weight is uint128;`. 原生类型包括 `int`,`uint`,`bytes32` 等 Solidity 本身定义的类型, 不包括由原生类型衍生出来的类型.

**自定义值类型的好处**:

1.  使用自定义值类型可增强类型安全性. 例如, 当定义了 `Weight` 和 `Price` 类型后, `Weight w = Weight.wrap(10);` 和 `Price p = Price.wrap(5);`, `Weight wp = w + p;` 和 `Price pw = p + w;` 会出现编译错误, 因为不同的自定义值类型不能进行算术运算, 避免了像直接用 uint128 类型可能出现的误用.

2.  提升代码可读性较为明显. 例如, 看到变量是 `Weight` 类型, 能直观知道它代表重量; 是 `Price` 类型, 就明白它表示价格.

**类型转换**:

自定义值类型和原生类型之间没有隐式类型转换, 需要通过显式调用转换函数实现:

-   `C.wrap`: 将原生类型转换成自定义值类型, 如 `Weight w = Weight.wrap(100);`.

-   `C.unwrap`: 将自定义值类型转换成原生类型, 如 `uint128 u = Weight.unwrap(w);`.

**自定义值类型不继承操作符**:

自定义值类型不会继承原生类型的操作符, 如 `+` `-` `*` `/` 等. 例如:

```solidity
Weight w1 = Weight.wrap(10);
Weight w2 = Weight.wrap(20);
Weight sum = w1 + w2; // 编译错误
```

如果需要对自定义值类型进行类似运算, 必须自己定义函数, 如:

```solidity
function add(Weight lhs, Weight rhs) public pure returns(Weight) {
    return Weight.wrap(Weight.unwrap(lhs) + Weight.unwrap(rhs));
}
```

<br><br>
